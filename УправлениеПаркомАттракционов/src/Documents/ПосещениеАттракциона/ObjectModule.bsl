// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Пользователи.ТекущийПользователь();
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.АктивныеПосещения.Записывать = Истина;
	Движения.Записать();
	
	ВидАттракционаДокумента = ВидАттракциона(Аттракцион);
	
	АктивныеПосещения = АктивныеПосещения(ВидАттракционаДокумента);
	
	Если АктивныеПосещения.КоличествоПосещенийОстаток < 1 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В билете не осталось посещений.";
		Сообщение.УстановитьДанные(ЭтотОбъект);
		Сообщение.Поле = "Основание";
		Сообщение.Сообщить();
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Выручка = АктивныеПосещения.СуммаОстаток / АктивныеПосещения.КоличествоПосещенийОстаток;
	
	// регистр АктивныеПосещения
	Движения.АктивныеПосещения.Записывать = Истина;
	Движение = Движения.АктивныеПосещения.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Основание = Основание;
	Движение.ВидАттракциона = ВидАттракционаДокумента;
	Движение.Номенклатура = АктивныеПосещения.Номенклатура;
	Движение.КоличествоПосещений = 1;
	Движение.Сумма = Выручка;

	// регистр ПосещенияАттракционов
	Движения.ПосещенияАттракционов.Записывать = Истина;
	ДвижениеПосещенияАттракционов = Движения.ПосещенияАттракционов.Добавить();
	ДвижениеПосещенияАттракционов.Период = Дата;
	ДвижениеПосещенияАттракционов.Аттракцион = Аттракцион;
	ДвижениеПосещенияАттракционов.Номенклатура = АктивныеПосещения.Номенклатура;
	ДвижениеПосещенияАттракционов.Клиент = КлиентПоОснованию();
	ДвижениеПосещенияАттракционов.Количество = 1;
	ДвижениеПосещенияАттракционов.Сумма = Выручка;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Активные посещения.
// 
// Возвращаемое значение:
//  Структура -  Активные посещения:
//  *Номенклатура - СправочникСсылка.Номенклатура   
//  *КоличествоПосещенийОстаток - Число
//  *СуммаОстаток - Число
//
Функция АктивныеПосещения(ВидАттракционаДокумента)
	
	Результат = Новый Структура;
	Результат.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	Результат.Вставить("КоличествоПосещенийОстаток", 0);
	Результат.Вставить("СуммаОстаток", 0);

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АктивныеПосещенияОстатки.КоличествоПосещенийОстаток,
		|	АктивныеПосещенияОстатки.Номенклатура,
		|	АктивныеПосещенияОстатки.СуммаОстаток
		|ИЗ
		|	РегистрНакопления.АктивныеПосещения.Остатки(, Основание = &Основание
		|	И ВидАттракциона = &ВидАттракциона) КАК АктивныеПосещенияОстатки";
		
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("ВидАттракциона", ВидАттракционаДокумента);
		
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ВидАттракциона(Аттракцион)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Аттракционы.ВидАттракциона
		|ИЗ
		|	Справочник.Аттракционы КАК Аттракционы
		|ГДЕ
		|	Аттракционы.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Аттракцион);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.ВидАттракциона;
	
КонецФункции

Функция КлиентПоОснованию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПродажаБилета.Клиент
	|ИЗ
	|	Документ.ПродажаБилета КАК ПродажаБилета
	|ГДЕ
	|	ПродажаБилета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Основание);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	Возврат Выборка.Клиент;	
	
КонецФункции

#КонецОбласти

#КонецЕсли
